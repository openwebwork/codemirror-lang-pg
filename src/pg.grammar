@precedence {
    arrayMember,
    hashMember,
    heredoc,
    noParens,
    parens,

    arrayFunction,

    arrowOperator @left,

    incdec @left,
    unary @right,
    exponent @right,
    binding @left,
    mult @left,
    plus @left,
    shift @left,
    compare @left,
    equal @left,
    namedUnary,
    binAnd @left,
    binOr @left,
    logAnd @left,
    logOr @left,
    range @left,

    ternary @right,
    goto @right,
    assign @right,

    pair @left,
    comma @left,

    not @right,
    and @left,
    or @left,

    op,
    list,
    listContents,
    prototype,
    attribute,
    named,

    bareword,
    option,
    packageVariable,
    packageName,
    constant,
    callExpressionName,

    loopControl,
    return @left,
    zeroOrMore,
    atLeastOne
}

@top Program { statement* }

statement[@isGroup=Statement] {
    PackageStatement { kw<"package"> PackageName Version? (semicolon | Block) } |
    ExpressionStatement { commaSep1<maybeParens<expression>> semicolon } |
    FunctionDefinition {
        (scope)?
        kw<"sub">
        FunctionName { !bareword Identifier }
        (FunctionPrototype FunctionAttribute* | FunctionAttribute* FunctionPrototypeAttribute FunctionAttribute*)?
        (semicolon | Block)
    } |
    SingleLineStatement { (expression | ReturnExpression) statementModifier semicolon } |
    IfStatement { kw<"if"> ParenthesizedArguments Block ElsifClause* ElseClause? } |
    UnlessStatement { kw<"unless"> ParenthesizedArguments Block ElsifClause* ElseClause? } |
    WhileStatement { Label? kw<"while"> ParenthesizedArguments Block Continue? } |
    UntilStatement { Label? kw<"until"> ParenthesizedArguments Block Continue? } |
    ForStatement1 {
        Label?
        (kw<"for"> | kw<"foreach">)
        ( "(" expression semicolon expression semicolon expression ")" | "(" semicolon semicolon ")")
        Block
    } |
    ForStatement2 {
        Label?
        (kw<"for"> | kw<"foreach">)
        (scope? ScalarVariable | scope? PGVariable | "~~" scope? HashVariable)?
        ParenthesizedArguments
        Block
        Continue?
    } |
    StandaloneBlock { Label? Block Continue? ~block semicolon? } |
    NamedBlockStatement { NamedBlock semicolon } |
    SpecialBlock { kw<"sub">? (kw<"BEGIN"> | kw<"UNITCHECK"> | kw<"CHECK"> | kw<"INIT"> | kw<"END">) Block } |
    Ellipsis { "..." } |
    EmptyStatement { ~block ";" } |
    PodStatement |
    EndDataStatement[@dynamicPrecedence=4] { (kw<"__END__"> | kw<"__DATA__">) endDataBlock } |
    EndDocument[@dynamicPrecedence=4] { kw<"ENDDOCUMENT"> endDataBlock } |
    LaTeXImageCode {
        (ScalarVariable | PGVariable | expression)
        ArrowOperator
        !arrowOperator
        LaTeXImageCodeStart
        endOfLine[@dynamicPrecedence=1] { ~block semicolon? }
    } |
    PGMLBlock |
    PGTextBlock
}

expression[@isGroup=Expression] {
    variable |
    TypeGlob |
    Option |
    string |
    Integer |
    Float |
    SpecialLiteral { kw<"__FILE__"> | kw<"__LINE__"> | kw<"__PACKAGE__"> } |
    Pair |
    listType |
    ArrayLength { "$#" (Identifier | ScalarVariable | PGVariable | "{" expression "}") } |
    ArrayRef |
    HashRef |
    ArrayAccessVariable {
        (ScalarVariable | PGVariable | expression)
        !arrayMember
        (ArrowOperator !arrowOperator)?
        arrayAccessor
    } |
    HashAccessVariable {
        (ScalarVariable | PGVariable | expression)
        !hashMember
        (ArrowOperator !arrowOperator)?
        hashAccessor
    } |
    PatternMatchM |
    PatternMatch |
    RegexQr |
    SubstitutionS |
    TransliterationTrOrY |
    ScalarDereference |
    ArrayDereference |
    HashDereference |
    FunctionDereference { "&" (ScalarDereference | ScalarVariable | PGVariable | "{" expression "}") } |
    TypeGlobDereference { "*" (ScalarDereference | ScalarVariable | PGVariable | "{" expression "}") } |
    Constant |
    VariableDeclaration { scope ("(" commaSep<maybeParens<variable | TypeGlob>> ")" | variable | TypeGlob) } |
    AnonymousFunction |
    Assignment { expression !assign "=" expression } |
    UpdateExpression { expression !assign (UpdateOp | kw<"x"> "=") expression } |
    BinaryExpression {
        expression !exponent ArithOp<"**"> expression |
        expression !binding BindingOp<"=~" | "!~"> expression |
        !op expression !mult "%" expression |
        expression !mult ("*" | "/" | kw<"x">) expression |
        expression !plus (ArithOp<"+"> | ArithOp<"-">) expression |
        expression !plus ConcatOp<"."> expression |
        expression !shift ("<<" | BitOp<">>">) expression |
        expression (
            !compare (CompareOp<"<" | ">" | "<=" | ">="> | kw<"lt"> | kw<"gt"> | kw<"le"> | kw<"ge">) expression
        )+ |
        expression (!equal (CompareOp<"==" | "!="> | kw<"eq"> | kw<"ne">) expression)+ |
        expression !equal (CompareOp<"<=>"> | kw<"cmp">) expression |
        expression !binAnd BitOp { "&" } expression |
        expression !binOr BitOp<"|" | "^"> expression |
        expression !logAnd LogicOp<"&&"> expression |
        expression !logOr LogicOp<"||" | "//"> expression |
        expression !range RangeOp<".." | "..."> expression |
        expression !and kw<"and"> expression |
        expression !or (kw<"or"> | kw<"xor">) expression
    } |
    UnaryExpression {
        (ArithOp<"+"> | ArithOp<"-"> | LogicOp<"~" | "!" | "~.">) !unary expression |
        toReference |
        kw<"not"> !not expression
    } |
    IncDecExpression[@name=UpdateExpression] {
        expression !incdec ArithOp<"++" | "--"> |
        ArithOp<"++" | "--"> !incdec expression
    } |
    NoArgumentNamedUnaryOperator { (kw<"time"> | kw<"times"> | kw<"wantarray">) ("(" ")")? } |
    NamedUnaryExpression { NamedUnaryOperator !namedUnary maybeParens<expression>? } |
    ListOperatorExpression { (ListOperator | builtinArrayFunction) (ParenthesizedArguments | Arguments) } |
    CallExpression {
        FunctionName {
            "&"?
            !callExpressionName
            (PackageName | PGOperator | !bareword Identifier ~identifier)
            ~methodName
        }
        (ParenthesizedArguments | Arguments)
    } |
    CallExpressionWithBareword { FunctionName { "&" (PackageName | !bareword Identifier) } } |
    Bless { kw<"bless"> maybeParens<reference (commaOperator expression)?> } |
    MethodInvocation {
        ((PackageName | StringSingleQuoted | ScalarVariable | PGVariable | expression) ~methodName)
        (
            ArrowOperator
            !arrowOperator
            (
                (kw<"SUPER"> "::")? FunctionName { !bareword Identifier } |
                SpecialScalarVariable |
                ScalarVariable |
                PGVariable |
                ScalarDereference |
                ParenthesizedArguments
            )
            ParenthesizedArguments?
        )
    } |
    IndirectMethodInvocation {
        (kw<"SUPER"> "::")? FunctionName { !bareword Identifier ~identifier }
        ((PackageName | ScalarVariable | PGVariable) ~methodName)
        ParenthesizedArguments?
    } |
    ArrayFunction {
        !arrayFunction
        (builtinArrayFunction | FunctionName { !bareword Identifier ~identifier })
        ~listOperator
        Block
        commaSep1<maybeParens<expression>>
    } |
    EvalExpression { Eval !namedUnary (Block | maybeParens<expression>?) } |
    DoExpression { kw<"do"> !namedUnary (Block | maybeParens<expression>?) } |
    GotoExpression { !goto kw<"goto"> (Identifier | expression) } |
    TernaryExpression { expression !ternary LogicOp<"?"> expression? ":" expression } |
    ArrayModifierFunction {
        arrayModifierFunction[@dynamicPrecedence=1] { kw<"push"> | kw<"unshift"> | kw<"splice"> }
        maybeParens<(ArrayVariable | ArrayDereference) commaOperator (ParenthesizedArguments | Arguments)>
    } |
    ArrayPopOrShift {
        arrayPopOrShift[@dynamicPrecedence=1] { kw<"pop"> | kw<"shift"> }
        maybeParens<(ArrayVariable | ArrayDereference)>?
    } |
    HashOrArrayDestructureFunction {
        destructureFunction[@dynamicPrecedence=1] { kw<"each"> | kw<"values"> | kw<"keys"> }
        maybeParens<ArrayVariable | ArrayDereference | HashVariable | HashDereference >
    }
}

PackageName { !packageName Identifier ("::" !packageName (UnrestrictedIdentifier | Identifier))* }
Label { Identifier ":" }
Constant { !constant Identifier }

FunctionPrototype { !prototype "(" Prototype? ")" }
FunctionPrototypeAttribute { ":" kw<"prototype"> FunctionPrototype }
FunctionAttribute { ":" (Identifier (!attribute ParenthesizedArguments)?) }

ParenthesizedArguments { "(" !parens commaSep<expression> ")" }
Arguments { !noParens commaSep1<expression> }

ReturnExpression[@dynamicPrecedence=1] { kw<"return"> !return commaSep<maybeParens<expression>> }

statementModifier {
    (kw<"if"> | kw<"unless"> | kw<"while"> | kw<"until"> | kw<"for"> | kw<"foreach">)
    (ParenthesizedArguments | Arguments)
}

ElsifClause { kw<"elsif"> ParenthesizedArguments Block }
ElseClause { kw<"else"> Block }
Continue { kw<"continue"> Block }

Block { "{" (statement | blockStatement)* "}" }
blockStatement {
    ReturnExpression semicolon |
    LoopControlStatement {
        !loopControl
        (kw<"next"> | kw<"last"> | kw<"redo">)
        Identifier?
        statementModifier?
        semicolon
    }
}

NamedBlock { (Identifier !named "{" (statement | ReturnExpression semicolon)* "}")+ }

PackageVariable {
    PackageName { basicVariable (~package "::" !packageName (UnrestrictedIdentifier | Identifier))* }
    ~package
    "::"
    !packageVariable
    PackageVariableName { UnrestrictedIdentifier | Identifier }
}

basicVariable[@dynamicPrecedence=1] {
    (ScalarVariable ~variable) |
    PGVariable |
    ArrayVariable |
    HashVariable
}

variable {
    basicVariable |
    SpecialScalarVariable |
    PackageVariable
}

listType { List | QWList }

quotedString { StringSingleQuoted | StringQQuoted | StringDoubleQuoted | StringQqQuoted }
string { quotedString | HeredocInitializer }

Pair { !pair ((!bareword Identifier) | (!option Option)) FatComma expression }
List { !list "(" !listContents commaSep<expression> ")" }

ArrayRef { "[" commaSep<expression> "]" }
HashRef { ArithOp<"+">? "{" commaSep<expression> "}" }

arrayAccessor { "[" expression "]" }
hashAccessor { "{" ((!bareword Identifier) | expression) "}" }

ScalarDereference { "$" (ScalarDereference | ScalarVariable | PGVariable | "{" expression "}") }
ArrayDereference { "@" (ScalarDereference | ScalarVariable | PGVariable | "{" expression "}") }
HashDereference { "%" (ScalarDereference | ScalarVariable | PGVariable | "{" expression "}") }

toReference { RefOp<"~~"> !unary expression }

AnonymousFunction {
    kw<"sub">
    (
        FunctionPrototype FunctionAttribute* |
        FunctionAttribute* FunctionPrototypeAttribute FunctionAttribute*
    )?
    Block
}

reference {
    ArrayRef |
    HashRef |
    ScalarVariable |
    PGVariable |
    toReference |
    AnonymousFunction
}

builtinArrayFunction { (kw<"grep"> | kw<"map"> | kw<"join"> | kw<"sort"> | kw<"unpack">) ~arrayFunction }

scope { kw<"our"> | kw<"state"> | kw<"my"> | kw<"local"> }

// This makes it so that a heredoc body is placed inside the ExpressionStatement containing the HeredocInitializer that
// starts it.  This is important because when a partial parse occurs this ensures the partial parse goes back far enough
// to see the HeredocInitializer and correctly parses the heredoc body.
@skip {} { StatementEnd { ";" (InterpolatedHeredocBody | UninterpolatedHeredocBody)* statementEnd } }
semicolon { StatementEnd | automaticSemicolon }

@skip {} {
    StringDoubleQuoted { '"' interpolationContent* '"' }
    StringQQuoted { kw<"q"> injectedComments? QuoteLikeStartDelimiter StringContent* QuoteLikeEndDelimiter }
    StringQqQuoted { kw<"qq"> injectedComments? QuoteLikeStartDelimiter interpolationContent* QuoteLikeEndDelimiter }
    QWList { kw<"qw"> injectedComments? QWListContent { QuoteLikeStartDelimiter QWElement* QuoteLikeEndDelimiter } }
    PatternMatchM {
        kw<"m">
        injectedComments?
        QuoteLikeStartDelimiter
        (StringContent | interpolationContent*)
        QuoteLikeEndDelimiter
        RegexOptions?
        regexEnd
    }
    PatternMatch { patternMatchStart "/" interpolationContent* "/" RegexOptions? regexEnd }
    RegexQr {
        kw<"qr">
        injectedComments?
        QuoteLikeStartDelimiter
        (StringContent | interpolationContent*)
        QuoteLikeEndDelimiter
        RegexOptions?
        regexEnd
    }
    SubstitutionS {
        kw<"s">
        injectedComments?
        QuoteLikeStartDelimiter
        (StringContent | interpolationContent*)
        quoteLikeMiddle
        (StringContent | interpolationContent*)
        QuoteLikeEndDelimiter
        RegexOptions?
        regexEnd
    }
    TransliterationTrOrY {
        (kw<"tr"> | kw<"y">)
        injectedComments?
        QuoteLikeStartDelimiter
        StringContent
        quoteLikeMiddle
        StringContent
        QuoteLikeEndDelimiter
        RegexOptions?
        regexEnd
    }

    quoteLikeMiddle { QuoteLikeSeparatorDelimiter | QuoteLikeEndDelimiter injectedComments? QuoteLikeStartDelimiter }

    injectedComments {
        (horizontalWhitespace | verticalWhitespace)+
        Comment
        ((horizontalWhitespace | verticalWhitespace)* Comment)*
    }

    // The uninterpolatedHeredocStart and interpolatedHeredocStart tokens invisible markers used to signal
    // (via canShift) where the UninterpolatedHeredocBody and InterpolatedHeredocBody start which
    // is at the first new line found after the HeredocInitializer.
    HeredocInitializer { !heredoc "<<" HeredocStartIdentifier }
    UninterpolatedHeredocBody { uninterpolatedHeredocStart StringContent? HeredocEndIdentifier }
    InterpolatedHeredocBody { interpolatedHeredocStart interpolationContent* HeredocEndIdentifier }

    // The afterInterpolation, packageNamePart, and afterPackageName tokens are invisible markers used to signal
    // (via canShift) that the tokenzier shouldn't consume things that the grammar should tokenize.
    interpolationContent {
        InterpolatedStringContent |
        EscapeSequence |
        basicVariable |
        SpecialScalarVariable |
        PackageVariable {
            PackageName { ScalarVariable (packageNamePart "::" (UnrestrictedIdentifier | Identifier))* }
            afterPackageName
            "::"
            PackageVariableName { UnrestrictedIdentifier | Identifier }
        } |
        ArrayAccessVariable |
        HashAccessVariable |
        ScalarDereference |
        ArrayDereference |
        UninterpolatedHeredocBody |
        InterpolatedHeredocBody
    }

    ArrayAccessVariable {
        (ScalarVariable | PGVariable | ArrayAccessVariable | HashAccessVariable)
        afterInterpolation?
        !arrayMember
        (ArrowOperator !arrowOperator)?
        arrayAccessor
    }
    HashAccessVariable {
        (ScalarVariable | PGVariable | ArrayAccessVariable | HashAccessVariable)
        afterInterpolation?
        !hashMember
        (ArrowOperator !arrowOperator)?
        hashAccessor
    }

    PodStatement { PodDirective PodContent PodCut }

    PGMLBlock { BeginPG PGMLContent EndPG }
    PGTextBlock { BeginPG PGTextContent EndPG }
}

@skip { Comment | horizontalWhitespace | verticalWhitespace | UninterpolatedHeredocBody | InterpolatedHeredocBody }

kw[@dynamicPrecedence=1]<term> { @extend[@name={term}]<Identifier, term> ~identifier }
Eval[@dynamicPrecedence=1] { @extend<Identifier, "eval"> ~identifier }

commaOperator { !comma (Comma | FatComma) }
commaSep<content> { !zeroOrMore (content (commaOperator content?)*)? }
commaSep1<content> { !atLeastOne content (commaOperator content?)* }

maybeParens<content> { !noParens content | "(" !parens content ")" }

@context contextTracker from "./tokens.js"
@external tokens semicolon from "./tokens" { automaticSemicolon, statementEnd }
@external tokens unrestrictedIdentifier from "./tokens" { UnrestrictedIdentifier }
@external tokens specialScalarVariable from "./tokens" { SpecialScalarVariable }
@external tokens builtinOperator from "./tokens" { NamedUnaryOperator, ListOperator, PGOperator }
@external specialize { ScalarVariable } pgVariable from "./tokens" { PGVariable }
@external tokens heredoc from "./tokens" {
    HeredocStartIdentifier,
    LaTeXImageCodeStart,
    uninterpolatedHeredocStart,
    interpolatedHeredocStart,
    HeredocEndIdentifier
}
@external tokens interpolated from "./tokens" {
    InterpolatedStringContent,
    EscapeSequence,
    afterInterpolation,
    packageNamePart,
    afterPackageName
}
@external tokens quoteLikeOperator from "./tokens" {
    QuoteLikeStartDelimiter
    QuoteLikeSeparatorDelimiter
    QuoteLikeEndDelimiter
    StringContent
    QWElement
}
@external tokens regex from "./tokens" { patternMatchStart, RegexOptions, regexEnd }
@external tokens pod from "./tokens" { PodDirective, PodContent, PodCut }
@external tokens endData from "./tokens" { endDataBlock }
@external tokens pgText from "./tokens" { BeginPG[closedBy=EndPG], PGMLContent, PGTextContent, EndPG[openedBy=BeginPG] }

@tokens {
    horizontalWhitespace { $[ \t] }
    verticalWhitespace { $[\n\r] }

    hex { $[0-9A-Fa-f] }
    oct { $[0-7] }

    separatedDigits { ("_" @digit+)* }

    exponent { $[eE] $[+-]? @digit+ separatedDigits }

    Float {
        @digit+ separatedDigits ("." @digit* separatedDigits exponent? | exponent) |
        "." @digit+ separatedDigits exponent?
    }

    Integer {
        @digit+ separatedDigits |
        "0" $[oO]? oct+ ("_" oct+)* |
        "0" $[xX] hex+ ("_" hex+)* |
        "0" $[bB] $[01]+ ("_" $[01]+)*
    }

    Version { "v" $[0-9.]+ | $[0-9.]+ | $[0-9._]+ }

    UpdateOp {
        (
            "**" | "*" | "/" | "%" | "+" | "-" | "." | "<<" | ">>" | "&" | "&." | "&&" |
            "^" | "^." | "|" | "||" | "|." | "//"
        )
        "="
    }

    Prototype { $[&$@%;*\[\]\\+]+ }

    CompareOp<term> { term }
    ArithOp<term> { term }
    BindingOp<term> { term }
    LogicOp<term> { term }
    RefOp<term> { term }
    BitOp<term> { term }
    ConcatOp<term> { term }
    RangeOp<term> { term }

    StringSingleQuoted[isolate] { ("'" (!['] | "~~'")* "'"?) }

    letter { $[_a-zA-Z] }
    Identifier { letter (letter | @digit)* }
    Option { "-" letter (letter | @digit)* }

    maybeBraces<content> {
        content |
        "{" (horizontalWhitespace | verticalWhitespace)* content (horizontalWhitespace | verticalWhitespace)* "}"
    }

    ScalarVariable { "$" maybeBraces<Identifier> }
    ArrayVariable { "@" $[+-_!] | "@" maybeBraces<Identifier> }
    HashVariable { "%" $[!+-] | "%" maybeBraces<Identifier> }
    TypeGlob { "*" maybeBraces<Identifier> ("::" maybeBraces<Identifier>)* }

    ArrowOperator { "->" }
    Comma { "," }
    FatComma { "=>" }

    Comment[isolate] { "#" ![\n]* }

    "{" "}" "[" "]" "(" ")" "$" "@" "%" "&" "*" "<<" "$#" ";" ":" "::" "~~" "=" "/"

    @precedence {
        StringSingleQuoted,
        Version,
        Float,
        Integer,
        RefOp<"~~">,
        Identifier,
        Option,
        ScalarVariable,
        ArrayVariable,
        HashVariable,
        TypeGlob,
        Prototype,
        "$#",
        UpdateOp,
        ConcatOp,
        ArithOp,
        "$",
        "@",
        "%",
        "&",
        "*",
        "/",
        "["
    }
}

@external propSource pgHighlighting from "./pg-highlighting"

@detectDelim
